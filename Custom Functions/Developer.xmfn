<?xml version="1.0" encoding="UTF-8"?><FMClipboard><Version><![CDATA[42062300]]></Version><Comment></Comment><SourceWindowName><![CDATA[Manage Custom Functions for “Anderson2X_data”]]></SourceWindowName><Class>XMFN</Class><ClipVersion>1</ClipVersion><ClipTimeStamp>3.491901e+9</ClipTimeStamp><ClipAuthor><![CDATA[darrenburgess]]></ClipAuthor><clipboardAsText><![CDATA[Developer(  )]]></clipboardAsText><Clipboard><fmxmlsnippet type="FMObjectList"><CustomFunction id="12" functionArity="0" visible="True" parameters="" name="Developer"><Calculation>/**
 * =====================================================
 * Developer ( )
 *		https://github.com/petrowsky/fmpstandards/blob/master/Functions/Developer.fmfn
 * 
 * PARAMETERS:
 *		none
 *
 * RETURNS:
 *		(bool) True or False based on proper   
 *		evaluation
 *
 * DEPENDENCIES:
 *		none
 *
 * NOTES:
 *		Because Filemaker Changed Get ( PrivilegeSetName )
 *		Evaluate () must be used
 *
 * RELEASE:
 *		2011-02-23
 * =====================================================
 * 
 */

Let ( [ 

	~developers = List ( "developer" ; "developer" );  // Add names of the accounts which are valid developer accounts
	~version = Get ( ApplicationVersion );
	~isGo = PatternCount ( ~version; "Go" ) = True;
	~versionNumber = GetAsNumber ( Substitute ( ~version ; "," ; "." ) ); // account for foreign versions using comma
	~extendedPrivileges = // Add "developer" extended privilege to groups which are valid developer groups
		If (  ~isGo or ~versionNumber ≥ 11;
			Evaluate ( "Get ( AccountExtendedPrivileges )" );
			/*else*/ Evaluate ( "Get ( ExtendedPrivileges )" )
		)

];

	PatternCount ( ¶&amp; ~developers &amp;¶ ; ¶&amp; Get ( AccountName ) &amp;¶ ) ≥ 1
	or PatternCount ( ¶&amp; ~extendedPrivileges &amp;¶ ; ¶&amp; "developer" &amp;¶ ) ≥ 1
	or
	If ( ~isGo or ~versionNumber ≥ 11;
		Evaluate ( "Get ( AccountPrivilegeSetName )" ) = "[Full Access]";
		/*else*/ Evaluate ( "Get ( PrivilegeSetName )" ) = "[Full Access]"
	)

)</Calculation></CustomFunction></fmxmlsnippet></Clipboard><Picture></Picture></FMClipboard>